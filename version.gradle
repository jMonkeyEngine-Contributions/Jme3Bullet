/*
 Version Info Examples
 =====================
 
 Nightly Build Snapshot
 * Full Name: jMonkeyEngine 3.1.0-5124
 * POM Version: 3.1.0-SNAPSHOT
 * NBM Version: 3.1.0.5124
 * NBM UC Suffix: nightly/3.1/plugins

 Nightly Build Snapshot (PBRIsComing branch)
 * Full Name: jMonkeyEngine 3.1.0-PBRIsComing-5124
 * POM Version: 3.1.0-PBRIsComing-SNAPSHOT
 * NBM Version: 3.1.0.5124
 * NBM UC Suffix: PBRIsComing-nightly/3.1/plugins

 Alpha1 Release
 * Full Name: jMonkeyEngine 3.1.0-alpha1
 * POM Version: 3.1.0-alpha1
 * NBM Version: 3.1.0.0
 * NBM UC Suffix: alpha1/3.1/plugins
 
 Final Release
 * Full Name: jMonkeyEngine 3.1.0
 * POM Version: 3.1.0
 * NBM Version: 3.1.0.0
 * NBM UC Suffix: stable/3.1/plugins
 */ 

import java.text.SimpleDateFormat
import org.ajoberstar.grgit.*

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.2.0'
    }
}

ext {
    jmeRevision     = 0
    jmeGitHash      = ""
    jmeGitTag       = ""
    jmeShortGitHash = ""
    jmeBuildDate    = new SimpleDateFormat("yyyy-MM-dd").format(new Date())
    jmeBranchName   = "unknown"
    jmeFullName     = "jMonkeyEngine ${jmeVersion}-UNKNOWN"
    jmePomVersion   = "unknown"
    jmeNbmVersion   = "0.0.0.0"
    jmeNbmUcSuffix  = "unknown"
}

task configureVersionInfo {
    try {
        def grgit = Grgit.open(project.file('.'))
        jmeRevision = grgit.log(includes:['HEAD']).size()
        jmeGitHash = grgit.head().id
        jmeShortGitHash = grgit.head().abbreviatedId
        jmeBranchName = grgit.branch.current.name
        jmeGitTag = grgit.describe()
        if (jmeGitTag == null) jmeGitTag = ""
        
        jmeFullName   = "jMonkeyEngine ${jmeVersion}"
        jmePomVersion = jmeVersion
        jmeNbmVersion = jmeVersion
        
        if (jmeBranchName != "master") {
            jmeFullName    += "-${jmeBranchName}"
            jmePomVersion  += "-${jmeBranchName}"
            
            jmeNbmUcSuffix = "${jmeBranchName}-"
        } else {
            jmeNbmUcSuffix = ""
        }
        
        if (jmeVersionTag == "SNAPSHOT") {
            jmeNbmUcSuffix += "nightly"
        } else if (jmeVersionTag == "") {
            jmeNbmUcSuffix += "stable"
        } else {
            jmeNbmUcSuffix += jmeVersionTag
        }
        
        jmeNbmUcSuffix += "/" + jmeMainVersion + "/plugins"
        
        if (jmeVersionTag == "SNAPSHOT") {
            jmeFullName   += "-${jmeRevision}"
            jmePomVersion += "-SNAPSHOT"
            jmeNbmVersion += ".${jmeRevision}"
        } else if (jmeVersionTag == "") {
            jmeNbmVersion += ".0"
        } else {
            jmeFullName   += "-${jmeVersionTag}"
            jmePomVersion += "-${jmeVersionTag}"
            jmeNbmVersion += ".0"
        }
        
        logger.warn("Full Name: ${jmeFullName}")
        logger.warn("POM Version: ${jmePomVersion}")
        logger.warn("NBM Version: ${jmeNbmVersion}")
        logger.warn("NBM UC Suffix: ${jmeNbmUcSuffix}")
    } catch (ex) {
        // Failed to get repo info
        logger.warn("Failed to get repository info: " + ex.message + ". " + \
                    "Only partial build info will be generated.")
    }
}
